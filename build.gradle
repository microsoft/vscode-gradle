plugins {
  id 'java'
  id 'com.google.protobuf' version '0.8.13'
  id 'org.sonarqube' version '3.0'
  id 'com.github.jlouns.cpe' version '0.5.0'
  id 'com.diffplug.spotless' version '5.7.0'
}

project.ext.set('grpcVersion', '1.30.0')
project.ext.set('protobufVersion', '3.12.0')
project.ext.set('protocVersion', project.protobufVersion)
project.ext.set('gradlingToolingApi', '6.4')

sonarqube {
  properties {
    property "sonar.projectKey", "badsyntax_vscode-gradle"
    property "sonar.organization", "badsyntax"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.env.SONAR_TOKEN
    property "sonar.github.oauth", System.env.GITHUB_TOKEN
  }
}

allprojects {
  group = 'vscode-gradle'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'com.google.protobuf'
  apply plugin: 'com.diffplug.spotless'

  repositories {
    maven {
      url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
    maven {
      url 'https://repo.gradle.org/gradle/libs-releases'
    }
    mavenCentral()
    mavenLocal()
  }

  protobuf {
    protoc {
      if (osdetector.os == 'osx') {
        // Required for arm64 support on MacOS
        artifact = "com.google.protobuf:protoc:${protocVersion}:osx-x86_64"
      } else {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
      }
    }
  }
}

task(format) {
  dependsOn(spotlessApply)
}
