plugins {
  id 'java'
  id 'com.google.protobuf' version '0.8.13'
  id 'com.github.jlouns.cpe' version '0.5.0'
  id 'com.diffplug.spotless' version '6.7.2'
}

project.ext.set('grpcVersion', '1.45.0')
project.ext.set('protobufVersion', '3.12.0')
project.ext.set('protocVersion', project.protobufVersion)
project.ext.set('toolingAPIVersion', '7.3')

allprojects {
  group = 'vscode-gradle'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'com.google.protobuf'
  apply plugin: 'com.diffplug.spotless'

  repositories {
    maven {
      url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
    maven {
      url 'https://repo.gradle.org/gradle/libs-releases'
    }
    mavenCentral()
    mavenLocal()
  }

  protobuf {
    protoc {
      if (osdetector.os == 'osx') {
        // Required for arm64 support on MacOS
        artifact = "com.google.protobuf:protoc:${protocVersion}:osx-x86_64"
      } else {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
      }
    }
  }
}

task(format) {
  dependsOn(':gradle-server:spotlessApply')
  dependsOn(':gradle-plugin:spotlessApply')
  dependsOn(':gradle-plugin-api:spotlessApply')
  dependsOn(':gradle-language-server:spotlessApply')
}
