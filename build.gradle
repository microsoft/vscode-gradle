plugins {
  id 'java'
  id 'com.google.protobuf' version '0.8.12'
  id 'org.sonarqube' version '2.8'
  id 'com.github.jlouns.cpe' version '0.5.0'
  id 'com.diffplug.gradle.spotless' version '4.3.0'
}

project.ext.set('grpcVersion', '1.29.0')
project.ext.set('protobufVersion', '3.11.0')
project.ext.set('protocVersion', project.protobufVersion)
project.ext.set('gradlingToolingApi', '6.3')

sonarqube {
  properties {
    property "sonar.projectKey", "badsyntax_vscode-gradle"
    property "sonar.organization", "badsyntax"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.env.SONAR_TOKEN
    property "sonar.github.oauth", System.env.GITHUB_TOKEN
  }
}

allprojects {
  group = 'vscode-gradle'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'com.google.protobuf'

  repositories {
    maven {
      url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
    maven {
      url 'https://repo.gradle.org/gradle/libs-releases'
    }
    mavenCentral()
    mavenLocal()
  }

  protobuf {
    protoc {
      artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
  }
}

task(format) {
  dependsOn(spotlessApply)
}
