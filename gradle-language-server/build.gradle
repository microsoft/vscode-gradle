plugins {
  id "java"
  id "application"
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

repositories  {
  mavenCentral()
  maven {
    url "https://groovy.jfrog.io/artifactory/plugins-release"
  }
}

application {
  applicationName = "gradle-language-server"
  mainClass = "com.microsoft.gradle.GradleLanguageServer"
}

dependencies {
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:0.12.0"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.12.0"
  implementation "org.codehaus.groovy:groovy-eclipse-batch:4.0.16-03"
  implementation "com.google.code.gson:gson:2.8.9"
  implementation "org.apache.bcel:bcel:6.6.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

ext.mainClass = "com.microsoft.gradle.GradleLanguageServer"

task copyJar(type: Copy) {
  from "build/libs/gradle-language-server.jar"
  from configurations.runtimeClasspath
  into "../extension/lib"
}

tasks.build.dependsOn tasks.copyJar
tasks.copyJar.dependsOn jar

jar {
  manifest {
    attributes(
      "Main-Class": "com.microsoft.gradle.GradleLanguageServer",
      "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
    )
  }
}

def mainJavaDir = 'src/main/java';
def mainResourcesDir = 'src/main/resources';

sourceSets {
  main {
    java {
      srcDirs mainJavaDir
    }
    resources {
      srcDirs mainResourcesDir
    }
  }
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    importOrder()
    eclipse()
    removeUnusedImports()
    trimTrailingWhitespace()
    targetExclude "build/**"
  }
}

compileJava.dependsOn 'spotlessCheck'

project.tasks.named("processResources") {
  duplicatesStrategy = 'include'
}
